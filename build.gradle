plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'jacoco'
}

group 'de.uni_passau.fim.se2'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.11

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'br.usp.each.saeg', name: 'asm-defuse', version: '0.0.6'
    implementation group: 'commons-cli', name: 'commons-cli', version: '1.4'
    implementation group: 'com.google.guava', name: 'guava', version: '29.0-jre'
    implementation group: 'org.jgrapht', name: 'jgrapht-core', version: '1.4.0'
    implementation group: 'org.ow2.asm', name: 'asm', version: '8.0'
    implementation group: 'org.ow2.asm', name: 'asm-analysis', version: '8.0'
    implementation group: 'org.ow2.asm', name: 'asm-tree', version: '8.0'
    implementation group: 'org.ow2.asm', name: 'asm-util', version: '8.0'

    testImplementation group: 'com.google.truth', name: 'truth', version: '1.0.1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.3.3'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.7'
    testImplementation group: 'org.hamcrest', name: 'hamcrest-library', version: '2.2'

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("${buildDir}/jacoco")
}

jacocoTestReport {
    reports {
        xml.enabled = false
        xml.setDestination(file("${buildDir}/jacoco/xml"))
        csv.enabled = true
        csv.setDestination(file("${buildDir}/jacoco/csv"))
        html.enabled = true
        html.setDestination(file("${buildDir}/jacoco/html"))
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    jacoco {
        enabled = true
    }
    finalizedBy(jacocoTestReport)
}

jar {
    archiveName = "${rootProject.name}-${rootProject.version}.jar"
    manifest {
        attributes(
                'Main-Class': 'de.uni_passau.fim.se2.SlicerMain',
                'Implementation-Title': "Slicer",
                'Implementation-Version': rootProject.version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version')
        )
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}
